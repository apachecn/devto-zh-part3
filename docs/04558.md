# ç”Ÿäº§ååº”åº”ç”¨çš„åŸå› ï¼ŸğŸ¤”(ç¬¬äºŒéƒ¨åˆ†)

> åŸæ–‡ï¼š<https://dev.to/seif_ghezala/reasonml-for-production-react-apps-part-2-362b>

æˆ‘å–œæ¬¢æ—¶ä¸æ—¶åœ°å»çœ‹ç†æ€§çš„è·¯çº¿å›¾ï¼Œå¯¹å³å°†è¢«çƒ¹é¥ªçš„ä¸œè¥¿æ„Ÿåˆ°å…´å¥‹ã€‚æ¯å½“æˆ‘è®¿é—®è¯¥é¡µé¢æ—¶ï¼Œæˆ‘éƒ½ä¼šæƒ…ä¸è‡ªç¦åœ°æ³¨æ„åˆ°è¿™ä¸¤ç‚¹:

[![ReasonML Roadmap](img/69f62591be3c1aaf1ea30d9df0b1beb8.png)](https://res.cloudinary.com/practicaldev/image/fetch/s--EeohSTev--/c_limit%2Cf_auto%2Cfl_progressive%2Cq_auto%2Cw_880/https://thepracticaldev.s3.amazonaws.com/i/4w5vix6ye46qjbtv9ku1.png) 

<figure>

<figcaption>æ¨ç†è·¯çº¿å›¾</figcaption>

</figure>

æ‰€ä»¥å®˜æ–¹æ–‡æ¡£æœ¬èº«æ‰¿è®¤å¼‚æ­¥æ•…äº‹å’Œ JSON å¤„ç†å¹¶æ²¡æœ‰é‚£ä¹ˆå¥½ã€‚

è¿™å°±æ˜¯ä¸ºä»€ä¹ˆåœ¨æœ¬ç³»åˆ—çš„ç¬¬äºŒéƒ¨åˆ†ä¸­ï¼Œæˆ‘ä»¬å°†ä¸ºå•è¯è®¡æ•°å™¨æ„å»ºä¸€ä¸ªç®€å•çš„ç™»å½•é¡µé¢ã€‚é€šè¿‡è¿™æ ·åšï¼Œæˆ‘å¾ˆç¡®å®šæˆ‘ä»¬ä¼šç¢°åˆ°è¿™ä¸¤ç‚¹ï¼Œå¹¶ä¸”å¯¹åˆç†æ„å»º React åº”ç”¨ç¨‹åºæœ‰ä¸€ä¸ªæ›´å¥½çš„æ¦‚è¿°ã€‚

å°±åƒå‰ä¸€éƒ¨åˆ†ä¸€æ ·ï¼Œæˆ‘å°†åœ¨è¿­ä»£ä¸­æ„å»ºè¿™ä¸ªç‰¹æ€§ï¼Œå¹¶æŠ¥å‘Šæˆ‘å–œæ¬¢/ä¸å–œæ¬¢ä»€ä¹ˆã€‚

[![Final Resul](img/9aad52cbd25224eae20c2d5e1426c9d9.png)](https://res.cloudinary.com/practicaldev/image/fetch/s--W2DB8rfW--/c_limit%2Cf_auto%2Cfl_progressive%2Cq_66%2Cw_880/https://thepracticaldev.s3.amazonaws.com/i/kqhk965ppigtor0dlppk.gif) 

<figure>

<figcaption>æœ€ç»ˆç»“æœ</figcaption>

</figure>

ä½ å¯ä»¥åœ¨è¿™é‡Œæ‰¾åˆ°æœ€ç»ˆä»£ç [ã€‚](https://github.com/siffogh/words-counter)

# è¿­ä»£#1:å°†å•è¯ counter åˆ†ç¦»æˆå®ƒè‡ªå·±çš„ç»„ä»¶

æˆ‘ä»¬çš„åº”ç”¨ç¨‹åºå°†ä¸å†åªæ˜¯ä¸€ä¸ªæ–‡å­—è®¡æ•°å™¨é¡µé¢ã€‚å› æ­¤ï¼Œæˆ‘ä»¬å¿…é¡»å°† App ç»„ä»¶çš„å†…å®¹ç§»åˆ°ä¸€ä¸ªå•ç‹¬çš„ç»„ä»¶ä¸­ï¼Œå¹¶ä¸ºç™»å½•é¡µé¢åˆ›å»ºå¦ä¸€ä¸ªç»„ä»¶ã€‚

```
/* src/Home.re */

[%bs.raw {|require('./Home.css')|}];

let countWordsInString = text => {
  let spacesRegex = Js.Re.fromString("\s+");

  switch (text) {
  | "" => 0
  | noneEmptyText =>
    noneEmptyText
    |> Js.String.trim
    |> Js.String.splitByRe(spacesRegex)
    |> Js.Array.length
  };
};

module Times = {
  [@bs.module "./icons/times.svg"] [@react.component]
  external make: (~height: string) => React.element = "default";
};

module Copy = {
  [@bs.module "./icons/copy.svg"] [@react.component]
  external make: (~height: string) => React.element = "default";
};

module CopyClipboard = {
  [@bs.module "react-copy-to-clipboard"] [@react.component]
  external make: (~text: string, ~children: React.element) => React.element =
    "CopyToClipboard";
};

[@react.component]
let make = () => {
  let (text, setText) = React.useState(() => "");

  let handleTextChange = e => ReactEvent.Form.target(e)##value |> setText;

  let handleClearClick = _ => setText(_ => "");

  let wordsCountText =
    (text |> countWordsInString |> string_of_int) ++ " words";

  <div className="Home">
    <div className="header">
      <h3> {"Words Counter" |> ReasonReact.string} </h3>
      <span> {ReasonReact.string(wordsCountText)} </span>
    </div>
    <textarea
      placeholder="Express yourself..."
      value=text
      onChange=handleTextChange
    />
    <div className="footer">
      <Button
        title="Clear text"
        onClick=handleClearClick
        disabled={String.length(text) === 0}>
        <Times height="20px" />
      </Button>
      <CopyClipboard text>
        <Button
          title="Copy text"
          disabled={String.length(text) === 0}
          category=Button.PRIMARY>
          <Copy height="20px" />
        </Button>
      </CopyClipboard>
    </div>
  </div>;
}; 
```

App ç»„ä»¶ä¹Ÿè¢«é‡æ„ï¼Œåªå‘ˆç°ä¸€ä¸ª Home ç»„ä»¶:

```
/* src/App.re */

[@react.component]
let make = () => {
  <div className="App"> <Home /> </div>;
}; 
```

> åˆ°ç›®å‰ä¸ºæ­¢ï¼Œæˆ‘ä»¬åªæ˜¯åšäº†ä¸€äº›å°æ”¹åŠ¨ï¼Œæ²¡æœ‰ä»€ä¹ˆè®©æˆ‘å–œæ¬¢/ä¸å–œæ¬¢çš„ã€‚

# è¿­ä»£ 2:æ·»åŠ è·¯ç”±å™¨

ä¸ºäº†èƒ½å¤Ÿå¯¼èˆªåˆ°æˆ‘ä»¬çš„ä¸»é¡µå’Œç™»å½•é¡µé¢ï¼Œæˆ‘ä»¬éœ€è¦æ·»åŠ ä¸€ä¸ªè·¯ç”±å™¨ã€‚

è®©æˆ‘ä»¬é¦–å…ˆæ„å»ºä¸€ä¸ªç®€å•çš„ç™»å½•é¡µé¢ï¼Œæˆ‘ä»¬å¯ä»¥å¯¼èˆªåˆ°:

```
/* src/Login.re */

[@react.component]
let make = () => {
  <div className="Login"> <h1>{ "Login Page" |> ReasonReact.string }</h1> </div>;
}; 
```

ReasonReact è‡ªå¸¦ä¸€ä¸ªå†…ç½®è·¯ç”±å™¨:ReasonReactRouterã€‚è·¯ç”±å™¨æä¾›äº†ä¸€ä¸ªé’©å­`useUrl()`ï¼Œç”¨æ¥æ¶ˆè€—å½“å‰çš„ urlã€‚ä¸ºäº†å®ç°å¯¼èˆªï¼Œæˆ‘ä»¬åªéœ€è¦æ ¹æ®æˆ‘ä»¬ä»`useUrl()`é’©å­:
ä½¿ç”¨çš„ url è·¯å¾„å‘ˆç°ä¸€ä¸ªä¸åŒçš„é¡µé¢ç»„ä»¶

```
/* src/App.re */

[@react.component]
let make = () => {
  let url = ReasonReactRouter.useUrl();

  let page =
    switch (url.path) {
    | [] => <Home />
    | ["login"] => <Login />
    | _ => <div> {"Page not found :/" |> ReasonReact.string} </div>
    };

  <div className="App"> page </div>;
}; 
```

## æˆ‘å–œæ¬¢ğŸ‘

*   å†…ç½®è·¯ç”±å™¨çš„ API éå¸¸ç®€å•ã€‚
*   url è·¯å¾„ä¸Šçš„æ¨¡å¼åŒ¹é…å°†å§‹ç»ˆæé†’æ‚¨ä¸è¦å¿˜è®°å›é€€é¡µé¢ï¼Œå¦åˆ™ï¼Œå®ƒæ˜¯ä¸è¯¦å°½çš„ã€‚
*   æˆ‘å¯ä»¥æƒ³è±¡ï¼ŒåŸºäºç‰¹å®šæ¡ä»¶çš„è·¯ç”±å¯ä»¥éå¸¸å®¹æ˜“å’Œä¼˜é›…åœ°å®ç°ï¼Œå› ä¸ºè·¯ç”±æ˜¯åŸºäºæ¨¡å¼åŒ¹é…çš„ã€‚

# è¿­ä»£ 3:æ„å»ºç™»å½•é¡µé¢ç•Œé¢

æˆ‘ä»¬å·²ç»è®¾ç½®äº†ä¸»é¡µå’Œç™»å½•é¡µé¢çš„å¯¼èˆªï¼Œæ‰€ä»¥è®©æˆ‘ä»¬å°†å®é™…å†…å®¹æ”¾å…¥ç™»å½•é¡µé¢ã€‚é¦–å…ˆï¼Œè®©æˆ‘ä»¬åˆ›å»ºä¸€ä¸ªå¸¦æœ‰ç”µå­é‚®ä»¶å’Œå¯†ç å­—æ®µçš„ç™»å½•æ¨¡æ¿ï¼Œè¿™äº›å­—æ®µå°†å®ƒä»¬çš„å€¼å­˜å‚¨åœ¨çŠ¶æ€:

```
/* src/Login.re */

[%bs.raw {|require('./Login.css')|}];

[@react.component]
let make = () => {
  let (email, setEmail) = React.useState(() => "");
  let (password, setPassword) = React.useState(() => "");

  let handleEmailChange = e => ReactEvent.Form.target(e)##value |> setEmail;
  let handlePasswordChange = e =>
    ReactEvent.Form.target(e)##value |> setPassword;

  let handleFormSubmit = e => {
    ReactEvent.Form.preventDefault(e);
    /* TODO: add logic for submitting the form */
  };

  <div className="Login">
    <h2> {"Login" |> ReasonReact.string} </h2>
    <form onSubmit=handleFormSubmit>
      <div className="inputField">
        <input
          type_="email"
          placeholder="Email"
          value=email
          onChange=handleEmailChange
          required=true
        />
      </div>
      <div className="inputField">
        <input
          type_="password"
          placeholder="Password"
          value=password
          onChange=handlePasswordChange
          required=true
        />
      </div>
      <Button title="Login" category=Button.PRIMARY>
        {"Login" |> ReasonReact.string}
      </Button>
    </form>
  </div>;
}; 
```

ä»¥ä¸‹æ˜¯ç™»å½•é¡µé¢çš„å¤–è§‚:

[![Login page](img/ebcb4bf8b77990102d455c8154bed2bf.png)](https://res.cloudinary.com/practicaldev/image/fetch/s--EXCbgBHp--/c_limit%2Cf_auto%2Cfl_progressive%2Cq_auto%2Cw_880/https://thepracticaldev.s3.amazonaws.com/i/ebknfeeju7pwnr2fdxe5.png) 

<figure>

<figcaption>ç™»å½•é¡µé¢</figcaption>

</figure>

## æˆ‘å–œæ¬¢ğŸ‘

*   æˆ‘å¯ä»¥è¯´æˆ‘ä½¿ç”¨ ReasonReact å˜å¾—ç›¸å½“å¿«ï¼Œå°½ç®¡æˆ‘ä¸å–œæ¬¢ä¸€äº›æˆ‘åœ¨å‰é¢éƒ¨åˆ†æåˆ°çš„ä¸œè¥¿(ä¾‹å¦‚ï¼Œå¿…é¡»ä½¿ç”¨ ReasonReact.string å’Œ ReactEventã€‚Form.target)ã€‚

# è¿­ä»£ 4:æ·»åŠ é”™è¯¯å¤„ç†

åœ¨è¿›å…¥è¡¨å•æäº¤å®ç°ä¹‹å‰ï¼Œæˆ‘å¸Œæœ›æˆ‘ä»¬çš„ç»„ä»¶é¦–å…ˆèƒ½å¤Ÿå¤„ç†æ½œåœ¨çš„é”™è¯¯ã€‚

ä¸ºæ­¤ï¼Œè®©æˆ‘ä»¬åˆ›å»ºä¸€ä¸ªå¤„ç† 3 ç§å¯èƒ½é”™è¯¯çš„[å˜é‡](https://reasonml.github.io/docs/en/variant#docsNav):

*   æœªæ‰¾åˆ°ç”µå­é‚®ä»¶
*   æ— æ•ˆå¯†ç 
*   å…¶ä»–(ä¾‹å¦‚ï¼Œç”¨æˆ·è¢«é˜»æ­¢æ—¶çš„é”™è¯¯)

```
type loginError =
  | EMAIL_NOT_FOUND
  | INVALID_PASSWORD
  | OTHER; 
```

å› ä¸ºæœåŠ¡å™¨é”™è¯¯æ¶ˆæ¯æ˜¯å­—ç¬¦ä¸²ï¼Œæ‰€ä»¥è®©æˆ‘ä»¬åˆ›å»ºä¸€ä¸ªåŠ©æ‰‹å‡½æ•°ï¼Œå°†å­—ç¬¦ä¸²è½¬æ¢æˆå˜é‡:

```
let stringToLoginError = str =>
  switch (str) {
  | "EMAIL_NOT_FOUND" => Some(EMAIL_NOT_FOUND)
  | "INVALID_PASSWORD" => Some(INVALID_PASSWORD)
  | _ => None
  }; 
```

ç°åœ¨ï¼Œæˆ‘ä»¬æƒ³åœ¨çŠ¶æ€ä¸­æ·»åŠ ä¸€ä¸ªé”™è¯¯å­—æ®µã€‚å› ä¸ºï¼Œåœ¨ä»»ä½•æ—¶å€™ï¼Œæˆ‘ä»¬è¦ä¹ˆæœ‰é”™è¯¯ï¼Œè¦ä¹ˆæ²¡æœ‰ï¼Œè¯¥å­—æ®µåº”è¯¥æ˜¯ option(loginError)ç±»å‹ã€‚

ä¸€æ—¦æœ‰äº†è¿™äº›ï¼Œä½¿ç”¨æ¨¡å¼åŒ¹é…æœ‰æ¡ä»¶åœ°å‘ˆç°ç”µå­é‚®ä»¶æˆ–å¯†ç é”™è¯¯å°±å˜å¾—éå¸¸å®¹æ˜“:

```
/* src/Login.re */

[%bs.raw {|require('./Login.css')|}];

type loginError =
  | EMAIL_NOT_FOUND
  | INVALID_PASSWORD
  | OTHER;

let stringToLoginError = str =>
  switch (str) {
  | "EMAIL_NOT_FOUND" => Some(EMAIL_NOT_FOUND)
  | "INVALID_PASSWORD" => Some(INVALID_PASSWORD)
  | _ => Some(OTHER)
  };

[@react.component]
let make = () => {
  let (email, setEmail) = React.useState(() => "");
  let (password, setPassword) = React.useState(() => "");
  let (error, setError) = React.useState(() => None);

  let handleEmailChange = e => ReactEvent.Form.target(e)##value |> setEmail;
  let handlePasswordChange = e =>
    ReactEvent.Form.target(e)##value |> setPassword;

  let handleFormSubmit = e => {
    ReactEvent.Form.preventDefault(e);
    /* TODO: add logic for submitting the form */
  };

  <div className="Login">
    <h2> {"Login" |> ReasonReact.string} </h2>
    <form onSubmit=handleFormSubmit>
      <div className="inputField">
        <input
          type_="email"
          placeholder="Email"
          value=email
          onChange=handleEmailChange
          required=true
        />
        {switch (error) {
         | Some(EMAIL_NOT_FOUND) =>
           <div className="error">
             {{js| âš   Email not found |js} |> ReasonReact.string}
           </div>
         | _ => ReasonReact.null
         }}
      </div>
      <div className="inputField">
        <input
          type_="password"
          placeholder="Password"
          value=password
          onChange=handlePasswordChange
          required=true
        />
        {switch (error) {
         | Some(INVALID_PASSWORD) =>
           <div className="error">
             {{js| âš  Invalid password |js} |> ReasonReact.string}
           </div>
         | _ => ReasonReact.null
         }}
      </div>
      <Button title="Login" category=Button.PRIMARY>
        {"Login" |> ReasonReact.string}
      </Button>
    </form>
  </div>;
}; 
```

## æˆ‘å–œæ¬¢ğŸ‘

*   åœ¨å¤„ç†ä¸åŒçš„é”™è¯¯ç±»å‹æ—¶ï¼Œå˜é‡ç»™äººä¿¡å¿ƒã€‚ä¾é å®ƒä»¬ï¼Œæ‚¨æœ€ç»ˆå¯ä»¥æ˜ç¡®åœ°å¤„ç†ä¸åŒç±»å‹çš„é”™è¯¯ã€‚

# è¿­ä»£ 5:ä¸ºè·å–åˆ›å»ºä¸€ä¸ªåŒ…è£…å™¨

å½“æˆ‘ä»¬æäº¤è¡¨å•æ—¶ï¼Œæˆ‘ä»¬å¸Œæœ›å‘åç«¯å‘å‡ºä¸€ä¸ªç™»å½• post è¯·æ±‚ã€‚å¦‚æœç”¨æˆ·å‡­è¯æœ‰é”™è¯¯ï¼Œæˆ‘ä»¬å·²ç»å‡†å¤‡å¥½å¤„ç†å®ƒäº†ğŸ˜ã€‚
å¦‚æœç™»å½•æˆåŠŸï¼Œæˆ‘ä»¬å°†è®°å½•ä¸€æ¡æˆåŠŸæ¶ˆæ¯ã€‚

å¦‚ä½•ä»æµè§ˆå™¨å‘å‡ºè¯·æ±‚ï¼Ÿä½ ä½¿ç”¨å…¨å±€å‡½æ•°`fetch`ã€‚

æˆ‘é€šå¸¸åœ¨ fetch ä¹‹ä¸Šåˆ›å»ºä¸€ä¸ªåŒ…è£…å™¨æ¥å‘å‡º POST/GET è¯·æ±‚ï¼ŒåŸå› å¦‚ä¸‹:

*   é¿å…é‡å¤çš„æ ·æ¿æ–‡ä»¶ã€‚
*   å½“è¯·æ±‚å¤±è´¥æ—¶ï¼Œè·å–æ‰¿è¯ºä¸ä¼šå¤±è´¥ã€‚å› æ­¤ï¼Œæˆ‘é€šå¸¸æ£€æŸ¥å“åº”çš„çŠ¶æ€ï¼Œå¹¶æ ¹æ®å®ƒæ¥è§£å†³æ‰¿è¯ºæˆ–ä½¿å…¶å¤±è´¥(å‚è§ä¸‹é¢çš„ä»£ç )ã€‚

ä¸‹é¢æ˜¯æˆ‘çš„ post è¯·æ±‚è·å–åŒ…è£…å™¨åœ¨ JavaScript ä¸­é€šå¸¸çš„æ ·å­:

```
export async function post(url, body) {
  let response = await fetch(url, {
    method: 'POST',
    credentials: 'include',
    body: typeof body === 'string' ? body : JSON.stringify(body),
    headers: {
      'Content-Type': 'application/json'
    },
    mode: 'cors'
  });

  if (response.status >= 200 && response.status < 300) {
    return response;
  }

  throw response;
} 
```

ç„¶åæˆ‘å¯ä»¥åœ¨å…¶ä»–ä»»ä½•åœ°æ–¹ä½¿ç”¨å®ƒ:

```
try {
    const response = await post('some/url', { foo: "bar"});
    const jsonContent = await response.json();
} catch(e) {
    // do something with the error
} 
```

è®©æˆ‘ä»¬è¯•ç€ç†æ™ºåœ°åšåŒæ ·çš„äº‹æƒ…ã€‚æ²¡æœ‰å†…ç½®çš„ç»‘å®šæ¥è·å– in Reasonï¼Œæ‰€ä»¥æˆ‘ä»¬å¿…é¡»ä½¿ç”¨ [bs-fetch](https://github.com/reasonml-community/bs-fetch) ã€‚

```
/* src/Request.re */

let post = (url, payload) => {
  let stringifiedPayload = payload |> Js.Json.object_ |> Js.Json.stringify;

  Fetch.fetchWithInit(
    url,
    Fetch.RequestInit.make(
      ~method_=Post,
      ~body=Fetch.BodyInit.make(stringifiedPayload),
      ~headers=Fetch.HeadersInit.make({"Content-Type": "application/json"}),
      (),
    ),
  );
}; 
```

bs-fetch æ¨¡å— fetch çš„ fetchWithInit å‡½æ•°ç”¨äºé€šè¿‡é…ç½®è°ƒç”¨ Fetchã€‚æˆ‘ä»¬å°† url å’Œç”¨`Fetch.RequestInit.make()`è¿›è¡Œçš„é…ç½®ä¼ é€’ç»™å®ƒã€‚

åˆ°ç›®å‰ä¸ºæ­¢ï¼Œè¿™æ˜¯ä¸€ä¸ªæ‰¿è¯ºã€‚

> æˆ‘æåˆ°è¿‡æˆ‘ä½¿ç”¨ Firebase ä½œä¸ºèº«ä»½éªŒè¯çš„åç«¯å—ï¼Ÿå—¯ï¼Œæˆ‘ä½¿ç”¨ Firebase ä½œä¸ºèº«ä»½éªŒè¯çš„åç«¯ã€‚

Firebase è¿”å›çš„è®¤è¯é”™è¯¯æ˜¯ JSON å¯¹è±¡ï¼Œæ‰€ä»¥æˆ‘ä»¬å¿…é¡»ä½¿ç”¨`response.json()`æ–¹æ³•ä»å“åº”ä¸­æå–é”™è¯¯å¯¹è±¡ã€‚

å› æ­¤ï¼Œæˆ‘ä»¬å¸Œæœ›ä» fetch å“åº”ä¸­æå– JSON å†…å®¹ã€‚ä¸ºæ­¤ï¼Œæˆ‘ä»¬å¯ä»¥ä½¿ç”¨ Fetchã€‚Response.json å‡½æ•°:

```
/* src/Request.re */

let post = (url, payload) => {
  let stringifiedPayload = payload |> Js.Json.object_ |> Js.Json.stringify;

  Fetch.fetchWithInit(
    url,
    Fetch.RequestInit.make(
      ~method_=Post,
      ~body=Fetch.BodyInit.make(stringifiedPayload),
      ~headers=Fetch.HeadersInit.make({"Content-Type": "application/json"}),
      (),
    ),
  ) |> Js.Promise.then_(Fetch.Response.json)
}; 
```

ç”±äºæˆ‘ä»¬å°†ç»å¸¸ä½¿ç”¨`then_``catch``resolve`å’Œ`reject`ï¼Œæˆ‘ä»¬å¯ä»¥æ‰“å¼€ Jsã€‚Promise æ¨¡å—ï¼Œè¿™æ ·æˆ‘ä»¬å°±ä¸ç”¨åœ¨å®ƒä»¬å‰é¢åŠ ä¸Š Jsã€‚æ‰“ç”µè¯æ—¶çš„æ‰¿è¯º:

```
/* src/Request.re */

let post = (url, payload) => {
  let stringifiedPayload = payload |> Js.Json.object_ |> Js.Json.stringify;

  Js.Promise.(
    Fetch.fetchWithInit(
      url,
      Fetch.RequestInit.make(
        ~method_=Post,
        ~body=Fetch.BodyInit.make(stringifiedPayload),
        ~headers=Fetch.HeadersInit.make({"Content-Type": "application/json"}),
        (),
      ),
    )
    |> then_(Fetch.Response.json)
  );
}; 
```

å°±åƒ JavaScript `json()`å‡½æ•°ä¸€æ ·ï¼Œ`Fetch.Response.json()`è¿”å›ä¸€ä¸ªæ‰¿è¯ºï¼Œè¯¥æ‰¿è¯ºé€šè¿‡ä¸€ä¸ªç±»å‹ä¸º`Js.Json`çš„å¯¹è±¡è¿›è¡Œè§£æ(å¦‚æœæ‚¨å°†é¼ æ ‡æ‚¬åœåœ¨å®ƒä¸Šé¢ï¼Œå¯ä»¥åœ¨ç¼–è¾‘å™¨ä¸­çœ‹åˆ°è¿™ä¸€ç‚¹)ã€‚

å¦‚æœæˆ‘ä»¬æƒ³ä»ç±»å‹ä¸º`Js.Json`çš„å¯¹è±¡ä¸­æå–ä»»ä½•æ•°æ®ï¼Œæˆ‘ä»¬å¿…é¡»ä½¿ç”¨å‡½æ•° Js å°†å®ƒè½¬æ¢æˆä¸€ä¸ª`Js.Dict` (dictionary/map)å¯¹è±¡ã€‚Json.decodeObject()ã€‚è¿™å°†è¿”å›ä¸€ä¸ªé€‰é¡¹ï¼Œå› ä¸º JSON å“åº”å¯ä»¥æ˜¯ç©ºçš„æˆ–è€…æ ¹æœ¬ä¸æ˜¯ä¸€ä¸ªå¯¹è±¡ã€‚å› æ­¤ï¼Œæˆ‘ä»¬å¿…é¡»ä½¿ç”¨æ¨¡å¼åŒ¹é…æ¥å¤„ç†è¿™ä¸¤ç§æƒ…å†µï¼Œå¹¶æå–`Js.Dict`å€¼:

```
/* src/Request.re */

let post = (url, payload) => {
  let stringifiedPayload = payload |> Js.Json.object_ |> Js.Json.stringify;

  Js.Promise.(
    Fetch.fetchWithInit(
      url,
      Fetch.RequestInit.make(
        ~method_=Post,
        ~body=Fetch.BodyInit.make(stringifiedPayload),
        ~headers=Fetch.HeadersInit.make({"Content-Type": "application/json"}),
        (),
      ),
    )
    |> then_(Fetch.Response.json)
    |> then_(response => {
      switch (Js.Json.decodeObject(response)) {
        | Some(decodedRes) => {
          resolve(decodedRes);
        }
        | None => resolve(Js.Dict.empty())
      }
    })
  );
}; 
```

ç°åœ¨æˆ‘ä»¬æœ‰äº†å“åº”å¯¹è±¡ï¼Œæˆ‘ä»¬éœ€è¦æ£€æŸ¥å®ƒæ˜¯å¦æœ‰é”™è¯¯ã€‚å¦‚æœå†…å®¹æœ‰ä¸€ä¸ªé”™è¯¯å¯¹è±¡ï¼Œæˆ‘ä»¬æ‹’ç»æ‰¿è¯ºï¼Œå¹¶æŠ›å‡ºä¸€ä¸ªåŒ…å«é”™è¯¯æ¶ˆæ¯çš„å¼‚å¸¸ã€‚å› æ­¤ï¼Œæˆ‘ä»¬å¿…é¡»ä¸ºæ­¤åˆ›å»ºä¸€ä¸ªå®šåˆ¶çš„å¼‚å¸¸ç±»å‹ã€‚å¦åˆ™ï¼Œæˆ‘ä»¬ç”¨åŒ…å«å“åº”çš„`Js.Dict`å¯¹è±¡æ¥è§£ææ‰¿è¯º:

```
/* src/Request.re */

exception PostError(string);

let post = (url, payload) => {
  let stringifiedPayload = payload |> Js.Json.object_ |> Js.Json.stringify;

  Js.Promise.(
    Fetch.fetchWithInit(
      url,
      Fetch.RequestInit.make(
        ~method_=Post,
        ~body=Fetch.BodyInit.make(stringifiedPayload),
        ~headers=Fetch.HeadersInit.make({"Content-Type": "application/json"}),
        (),
      ),
    )
    |> then_(Fetch.Response.json)
    |> then_(response =>
         switch (Js.Json.decodeObject(response)) {
         | Some(decodedRes) =>
           switch (Js.Dict.get(decodedRes, "error")) {
           | Some(error) =>
             reject(
               PostError(
                 "There is an error, but we don't know its message yet",
               ),
             )

           | None => resolve(decodedRes)
           }
         | None => resolve(Js.Dict.empty())
         }
       )
  );
} 
```

æˆ‘ä»¬ç°åœ¨è¦åšçš„å°±æ˜¯ä» Js ä¸­æå–é”™è¯¯æ¶ˆæ¯ã€‚Json é”™è¯¯ï¼Œä¸æˆ‘ä»¬ä»å“åº”ä¸­æå–é”™è¯¯å¯¹è±¡çš„æ–¹å¼ç›¸åŒã€‚å”¯ä¸€çš„åŒºåˆ«æ˜¯æ¶ˆæ¯æ˜¯ä¸€ä¸ª`string`è€Œä¸æ˜¯ä¸€ä¸ªå¯¹è±¡:

```
/* src/Request.re */

exception PostError(string);

let post = (url, payload) => {
  let stringifiedPayload = payload |> Js.Json.object_ |> Js.Json.stringify;

  Js.Promise.(
    Fetch.fetchWithInit(
      url,
      Fetch.RequestInit.make(
        ~method_=Post,
        ~body=Fetch.BodyInit.make(stringifiedPayload),
        ~headers=Fetch.HeadersInit.make({"Content-Type": "application/json"}),
        (),
      ),
    )
    |> then_(Fetch.Response.json)
    |> then_(response =>
         switch (Js.Json.decodeObject(response)) {
         | Some(decodedRes) =>
           switch (Js.Dict.get(decodedRes, "error")) {
           | Some(error) =>
             switch (Js.Json.decodeObject(error)) {
             | Some(decodedErr) =>
               switch (Js.Dict.get(decodedErr, "message")) {
               | Some(errorMessage) =>
                 switch (Js.Json.decodeString(errorMessage)) {
                 | Some(decodedErrorMessage) =>
                   reject(PostError(decodedErrorMessage))
                 | None => reject(PostError("POST_ERROR"))
                 }
               | None => resolve(decodedRes)
               }
             | None => resolve(decodedRes)
             }

           | None => resolve(decodedRes)
           }
         | None => resolve(Js.Dict.empty())
         }
       )
  );
}; 
```

å°±è¿™æ ·å§ï¼Œä¸‹æ¬¡è¿­ä»£çœ‹çœ‹æˆ‘ä»¬çš„æ¨¡å—è¡Œä¸è¡Œã€‚

ä½†åœ¨æ­¤ä¹‹å‰ï¼Œæˆ‘å–œæ¬¢/ä¸å–œæ¬¢çš„æ˜¯:

## æˆ‘å–œæ¬¢ğŸ‘

*   Jsã€‚æ‰¿è¯ºæ€»æ˜¯æœ‰å›æŠ¥çš„ã€‚è¿™ä½¿å¾—å®ƒä»¬çš„è¿”å›å€¼æ¯” JavaScript ä¸­çš„æ‰¿è¯ºæ›´å®¹æ˜“é¢„æµ‹ï¼ŒJavaScript ä¸­çš„æ‰¿è¯ºæ—¢å¯ä»¥è¿”å›æ‰¿è¯ºï¼Œä¹Ÿå¯ä»¥è¿”å›å€¼ã€‚

*   Jsã€‚Post æ€»æ˜¯è¿”å›ä¸€ä¸ªç”¨ Js.Dict è§£æçš„æ‰¿è¯ºã€‚ä½¿ç”¨å®ƒå¾ˆéš¾å‡ºé”™ï¼Œå› ä¸ºå®ƒçš„è¿”å›ç±»å‹æ˜¯å¯é¢„æµ‹çš„ã€‚

*   è™½ç„¶ä»å“åº”ä¸­æå–é”™è¯¯ä¿¡æ¯æ˜¯ä¸€ä¸ªçœŸæ­£çš„æ–—äº‰ï¼Œä½†æˆ‘ä¸å¾—ä¸æ‰¿è®¤å®ƒæ˜¯é˜²å¼¹çš„ã€‚æˆ‘ç”šè‡³åœ¨çœ‹æµè§ˆå™¨ä¹‹å‰å°±è®¾æ³•è®©å®ƒå·¥ä½œäº†ã€‚

## æˆ‘ä¸å–œæ¬¢ğŸ‘

*   bs-fetch API çš„è¯­æ³•å¾ˆä¸è‡ªç„¶ã€‚
*   è™½ç„¶è®¿é—® JSON å€¼æ˜¯å®‰å…¨çš„ï¼Œä½†ä¸å¯å¦è®¤è¿™æ˜¯ä¸€ç§ç—›è‹¦ï¼Œå¾ˆå®¹æ˜“å¯¼è‡´éš¾ä»¥ç†è§£çš„åµŒå¥—åœ°ç‹±ã€‚

## è¿­ä»£#6:åœ¨ç™»å½•é¡µé¢ä¸­ä½¿ç”¨è¯·æ±‚æ¨¡å—

æˆ‘ä»¬å·²ç»æ„å»ºäº†ä¸€ä¸ªå¾ˆå¥½çš„æ¨¡å—æ¥è¿›è¡Œ POST è¯·æ±‚ï¼Œè®©æˆ‘ä»¬åœ¨ç™»å½•é¡µé¢ä¸­ä½¿ç”¨å®ƒã€‚

åœ¨è¿™æ ·åšä¹‹å‰ï¼Œæˆ‘ä»¬å¿…é¡»å¤„ç†ä¸€ä¸ªé—®é¢˜:æˆ‘ä»¬ä¸èƒ½ä»è¢«æ‹’ç»çš„æ‰¿è¯ºä¸­ç›´æ¥è®¿é—®ä¸€ä¸ªå¼‚å¸¸ã€‚è®©æˆ‘ä»¬æŒ‰ç…§[å®˜æ–¹ BuckleScript ç¤ºä¾‹](https://bucklescript.github.io/docs/en/interop-misc#safe-external-data-handling)æ„å»ºä¸€ä¸ªå°çš„åŠ©æ‰‹å‡½æ•°ï¼Œä»è¢«æ‹’ç»çš„æ‰¿è¯ºä¸­æå–æˆ‘ä»¬çš„å¼‚å¸¸:

```
 let handlePromiseFailure =
    [@bs.open]
    (
      fun
      | Request.PostError(err) => {
          err;
        }
    ); 
```

æˆ‘ä»¬ç°åœ¨å¯ä»¥åœ¨`handleFormSubmit`å‡½æ•°ä¸­è¿›è¡Œ POST è°ƒç”¨:

```
/* src/Login.re */

  let handleFormSubmit = e => {
    ReactEvent.Form.preventDefault(e);
    let payload = Js.Dict.empty();
    Js.Dict.set(payload, "email", Js.Json.string(email));
    Js.Dict.set(payload, "password", Js.Json.string(password));

    Js.Promise.(
      Request.post(loginUrl, payload)
      |> then_(res =>
           {
             alert("Login successful!");
             setError(_ => None);
           }
           |> resolve
         )
      |> catch(e =>
           (
             switch (handlePromiseFailure(e)) {
             | Some(err) => setError(_ => stringToLoginError(err))
             | None => setError(_ => None)
             }
           )
           |> resolve
         )
      |> ignore
    );
  }; 
```

ä¸ºäº†æµ‹è¯•ç™»å½•è¡¨å•ï¼Œæˆ‘åˆ›å»ºäº†ä»¥ä¸‹ç”¨æˆ·:

```
{  "email":  "demo@demo.com",  "password":  "demodemo"  } 
```

è¿™æ˜¯æœ€åçš„ç»“æœ:

[![Final Login form](img/9aad52cbd25224eae20c2d5e1426c9d9.png)](https://res.cloudinary.com/practicaldev/image/fetch/s--W2DB8rfW--/c_limit%2Cf_auto%2Cfl_progressive%2Cq_66%2Cw_880/https://thepracticaldev.s3.amazonaws.com/i/kqhk965ppigtor0dlppk.gif) 

<figure>

<figcaption>æœ€ç»ˆç™»å½•è¡¨å•</figcaption>

</figure>

å°±æ˜¯è¿™æ ·ï¼ç™»å½•é¡µé¢å®Œå…¨æŒ‰ç…§æˆ‘ä»¬æƒ³è¦çš„é‚£æ ·å·¥ä½œï¼ğŸ‰

## æˆ‘ä¸å–œæ¬¢ğŸ‘

ä»è¢«æ‹’ç»çš„æ‰¿è¯ºä¸­æå–ä¾‹å¤–çš„é¢å¤–æ­¥éª¤ã€‚