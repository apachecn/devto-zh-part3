# æˆ‘ç¬¬ä¸€æ¬¡ä½¿ç”¨ SPARK-Ada

> åŸæ–‡ï¼š<https://dev.to/pinotattari/my-first-experience-with-spark-ada-2onp>

æˆ‘ç”¨ Ada è¯­è¨€ç¼–ç¨‹ï¼Œæˆ‘å–œæ¬¢å®ƒã€‚

> æ˜¯çš„ï¼Œæˆ‘çŸ¥é“ï¼Œå¬èµ·æ¥åƒæ˜¯â€œå‡ºæŸœâ€...ğŸ˜‰

åœ¨ Ada çš„ä¸–ç•Œé‡Œï¼Œæœ‰ä¸€ä»¶äº‹æˆ‘ä¸€ç›´æƒ³å°è¯•:å®ƒæ˜¯ Ada çš„ä¸€ä¸ªç²¾ç®€å­é›†ï¼Œå«åš *SPARK* (ä¸ Apache æ— å…³)ï¼Œå®ƒå…è®¸*å½¢å¼æ£€æŸ¥*ï¼Œä¹Ÿå°±æ˜¯è¯´ï¼Œ*è¯æ˜*ä½ çš„ä»£ç çš„ä¸€äº›å±æ€§ï¼Œæ¯”å¦‚:æ²¡æœ‰ç¼“å†²åŒºæº¢å‡ºå’Œå…¶ä»–è¿è¡Œæ—¶é”™è¯¯ï¼Œæ²¡æœ‰æœªåˆå§‹åŒ–çš„å˜é‡è¯»å–ï¼Œç­‰ç­‰ã€‚æˆ‘å–œæ¬¢ç”¨æ•°å­¦è¯æ˜æ¥æ­¦è£…æˆ‘çš„ä»£ç (æˆ–è€…è‡³å°‘æ˜¯å®ƒçš„ä¸€éƒ¨åˆ†)ğŸ˜Šã€‚

> å¥½å¥‡ SPARKï¼Ÿçœ‹çœ‹è¿™ç¯‡[ä»‹ç» SPARK](https://learn.adacore.com/courses/intro-to-spark/index.html) çš„æ–‡ç« 

å‡ å¤©å‰ï¼Œæˆ‘å†³å®šç”¨æˆ‘çš„ä¸€ä¸ªæ—§å‡½æ•°æ¥è¯•è¯•:ä¸€ä¸ªç±»ä¼¼ Ruby çš„`split`åœ¨åˆ†éš”ç¬¦å¤„åˆ†å‰²å­—ç¬¦ä¸²ã€‚è¿™æ˜¯æˆ‘ä¸ä¹…å‰å†™çš„ä¸€å°æ®µä»£ç ï¼Œåœ¨æˆ‘çš„è®¸å¤šç¨‹åºä¸­å®‰å…¨åœ°ä½¿ç”¨è¿‡:å®éªŒç”¨çš„æ­£ç¡®çš„â€œ*è±šé¼ *â€ã€‚

> å¦‚æœä½ å¥½å¥‡ï¼Œæœ€åçš„ç«èŠ±ç åœ¨ [github](https://github.com/fintatarta/ada-tokenizer) ä¸Šï¼Œè¯·ä¾¿ï¼ğŸ˜Š

é¦–å…ˆè¦åšçš„æ˜¯ç”¨æ™®é€šæ•°ç»„æ›¿æ¢ä½¿ç”¨`Vectors`(ä¸€ç§åŠ¨æ€æ•°ç»„ï¼Œç±»ä¼¼äºå…¶å¯¹åº”çš„`C++`)ã€‚ä½ çœ‹ï¼Œä½ å¯ä»¥æƒ³è±¡ï¼Œ`Vectors`ä½¿ç”¨æŒ‡é’ˆ(*è®¿é—®ç±»å‹*åœ¨ Ada è¡Œè¯ä¸­)å¹¶ä¸”å®ƒä»¬åœ¨ SPARK ä¸­æ˜¯*ç¦æ­¢çš„*ã€‚äº‹å®ä¸Šï¼ŒæŒ‡é’ˆå°†å…è®¸å¯¹è±¡çš„*åˆ«å*ï¼Œè¿™å°†ä½¿å½¢å¼æ£€æŸ¥å˜å¾—ä¸å¯èƒ½ã€‚

> è¿™å¹¶ä¸åƒå¬èµ·æ¥é‚£ä¹ˆç³Ÿç³•:åœ¨ Ada ä¸­ï¼Œç›¸å¯¹äº`C`æˆ–`C++`ï¼Œä½¿ç”¨æŒ‡é’ˆçš„å¿…è¦æ€§å¤§å¤§é™ä½äº†ã€‚å”¯ä¸€çœŸæ­£éœ€è¦çš„æ˜¯å½“ä½ æƒ³åŠ¨æ€åˆ†é…ç»“æ„çš„æ—¶å€™ã€‚

å› æ­¤ï¼Œæˆ‘ç”¨æ‰€éœ€åŠŸèƒ½çš„æœ€å°é›†åˆç¼–å†™äº†è‡ªå·±çš„ä¼ª`Vector`ç±»å‹ã€‚æƒ³æ³•æ˜¯å°†æå–çš„ç‰‡æ®µä¿å­˜åœ¨ä¸€ä¸ªè¶³å¤Ÿå¤§çš„å›ºå®šæ•°ç»„ä¸­(åˆ†é…ç»™å †æ ˆï¼Œå› æ­¤æ²¡æœ‰æ˜¾å¼æŒ‡é’ˆ)ï¼Œä»¥ä¿å­˜ç‰‡æ®µ(å¦‚æœä½ `split`ä¸€ä¸ªé•¿åº¦ä¸º`N`çš„å­—ç¬¦ä¸²ï¼Œä½ æœ€å¤šå¾—åˆ°`N+1`ä¸ªç‰‡æ®µ)ã€‚ä¹Ÿè®¸ä¸æ˜¯éå¸¸æœ‰æ•ˆçš„å†…å­˜ï¼Œä½†å¾ˆå¥½åœ°æ»¡è¶³äº†æˆ‘çš„éœ€æ±‚ã€‚

äº§ç”Ÿçš„ä»£ç (å‡ºäºâ€œæ•™å¯¼â€çš„ç›®çš„ï¼Œç›¸å¯¹äºåŸå§‹ä»£ç ç¨ä½œåˆ å‡...)å¦‚ä¸‹ã€‚è¿™çœ‹èµ·æ¥å¾ˆé•¿ï¼Œä½†æˆ‘åªæ˜¯æ·»åŠ äº†ä¸€äº›æ³¨é‡Šæ¥è§£é‡Šæ›´ Ada é£æ ¼çš„è¯­æ³•ã€‚

é¦–å…ˆæ˜¯*è§„èŒƒ*éƒ¨åˆ†(éå¸¸ç²—ç•¥åœ°ç›¸å½“äºä¸€ä¸ª`*.h`æ–‡ä»¶)

```
with Ada.Strings.Unbounded;     use Ada.Strings.Unbounded;

package Token_Lists 
   with SPARK_Mode => On -- This specs is in SPARK
is

   subtype List_Length is Integer range 1 .. Integer'Last;

   -- "Tagged types" correspond roughly to what other 
   -- languages call "class".  Token_List is the desired
   -- pseudo-vector for holding the pieces. 
   --
   -- The funny (<>) means that the type is parameterized, but the
   -- parameter is not public.  This forces you to initialize 
   -- any variable of type Token_List with some kind of constructor.
   --
   -- Finally, "private" means that the internal details are not
   -- visible and are described later, in the private part. 
   --
   type Token_List (<>) is tagged private;

   function Create (N : List_Length) return Token_List
     with
       Post =>
         Create'Result.Capacity = N and Create'Result.Length = 0;
    --
    -- Post is an "attribute" and it specifies a Boolean expression
    -- that will be true when the function returns (contract). 
    --
    -- The post-condition says that the created list will have 
    -- room for N entries and it will be empty
    --

   function Capacity (Item : Token_List) return Positive;

   function Length (Item : Token_List) return Natural
     with Post => Length'Result <= Item.Capacity;
   --
   -- Number of the pieces currently in the list.  Of course 
   -- it cannot be larger than the capacity, if it happens 
   -- there is something wrong somewhere...
   ---

   procedure Append (List : in out Token_List;
                     What : String)
     with
       Pre'Class =>
         List.Length  < List.Capacity,

       Post =>
         List.Length = List.Length'Old + 1
     and List.Capacity = List.Capacity'Old;
    -- 
    -- The precondition (attribute "Pre") says that before calling
    -- the procedure there must be some room in the list; the
    -- post condition says that after the call there is a new entry,
    -- but the capacity is unchanged (it is obvious to you, not to
    -- SPARK).  SPARK will analyze the body of Append and it 
    -- will try to prove that the contract is respected, that is, 
    -- that the postcondition follows from the precondition.
    --
    -- If you arm correctly the code that calls Append, SPARK will
    -- try to prove that the precondition is always verified. If it 
    -- succeeds you know you'll never have an overflow!
    --

private
   --
   -- Some privacy needed... :-)
   --

   type Token_Array is array (Positive range <>) of Unbounded_String;
   --
   -- The funny "(Positive range <>)" says that the index of 
   -- indexes of a variable of type Token_Array is a range of
   -- positive integers. 
   -- 
   -- Yes, in Ada the array indexes do not *necessarily* start
   -- from zero... They can start from whatever you want... ;-D
   --

   type Token_List (Length : List_Length) is tagged
      record
         Tokens     : Token_Array (1 .. Length) := (others => Null_Unbounded_String);
         First_Free : Positive := 1;
      end record
     with 
       Predicate => 
         Token_List.First_Free <= Integer (Token_List.Length) + 1
     and Token_List.Tokens'Length = Token_List.Length;
    -- 
    -- This is the full definition of Token_List anticipated 
    -- in the public part above.  In Ada it is not possible 
    -- to have some field public and some private (like in C++
    -- or Ruby).  If you put the full definition in the public
    -- part, everything is public (typically considered bad practice),
    -- otherwise everything is private.  Honestly, I never felt the
    -- need of the hybrid solution.
    -- 
    -- "Predicate" is another attribute.  It specifies a condition
    -- that a variable of type Token_List must always satisfy.
    --
    -- If you ask to the compiler to check assertions, the 
    -- compiler will produce code that checks the predicate
    -- at the end of methods ("primitive procedures" in Ada 
    -- jargon) that modify the variable and if the condition 
    -- is not satisfied, an exception is raised, pointing an
    -- accusing finger against the culprit. 
    --
    -- A powerful bug trap!
    --

   function Create (N : List_Length) return Token_List
   is (Token_List'(Length     => N,
                   Tokens     => (others => Null_Unbounded_String),
                   First_Free => 1));

   function Capacity (Item : Token_List) return Positive
   is (Item.Tokens'Last);

   function Length (Item : Token_List) return Natural
   is (Item.First_Free - 1);
end Token_Lists; 
```

ç°åœ¨çš„*ä½“*(æˆ–å¤šæˆ–å°‘ç›¸å½“äº`*.c` )

```
pragma Ada_2012;
package body Token_Lists 
   with SPARK_Mode => On -- This body is in SPARK
is

   ------------
   -- Append --
   ------------

   procedure Append
     (List : in out Token_List;
      What : String)
   is
   begin
      --
      -- If you look at the definition of Token_List 
      -- you'll see that List.Tokens is an array. 
      -- List.Tokens'Last is the last index of Lists.Tokens
      -- 
      if List.First_Free > List.Tokens'Last then
         raise Constraint_Error;
      end if;

      List.Tokens (List.First_Free) := To_Unbounded_String (What);
      List.First_Free := List.First_Free + 1;
   end Append;

end Token_Lists; 
```

å³ä½¿ä½ ä¸çŸ¥é“é˜¿è¾¾ï¼Œæˆ‘æƒ³ä½ åº”è¯¥èƒ½ç†è§£ä»£ç ã€‚

å¦‚ä½ æ‰€è§ï¼Œ`Append`éå¸¸ç®€å•ï¼Œæ˜¾ç„¶æ˜¯æ­£ç¡®çš„ï¼Œå¯¹å—ï¼Ÿæˆ‘çš„æ„æ€æ˜¯ï¼Œå¼€å¤´çš„`if`åœæ­¢äº†æ‰€æœ‰è¶…å‡ºæ•°ç»„`List.Tokens`çš„è¯•æ¢æ€§å†™å…¥ï¼Œå¯¹å—ï¼Ÿ

SPARK æŠ±æ€¨è¯´ï¼Œå®ƒæ— æ³•è¯æ˜æº¢å‡ºä¸ä¼šå‘ç”Ÿã€‚æˆ‘çš„ç¬¬ä¸€ååº”æ˜¯ç±»ä¼¼äºâ€œä½ ç–¯äº†å—ï¼Ÿä½ æ²¡çœ‹åˆ°æ”¯ç¥¨ä¸Šé¢çš„ç¬¬å››è¡Œå—ï¼Ÿä½ æ€ä¹ˆäº†ï¼Ÿâ€

æ— è®ºå¦‚ä½•ï¼Œé”™è¯¯ä¿¡æ¯æ—è¾¹æœ‰ä¸€ä¸ªâ€œæ”¾å¤§é•œâ€å›¾æ ‡ã€‚æˆ‘ç‚¹å‡»å®ƒï¼Œæˆ‘å¾—åˆ°äº†ï¼Œç«èŠ±çš„ç¤¼é‡ï¼Œä¸€ä¸ª*åä¾‹*

```
 [Counterexample] List = (Length => ?, First_Free => 2147483647) 
 and List.Tokens'First = 1 and List.Tokens'Last = 2147483647 
```

ä»€ä¹ˆä¸œè¥¿...ï¼Ÿå•Šï¼...æ˜¯çš„ï¼Œå¥½å§ï¼Œä½ æ˜¯å¯¹çš„...

ä»¥é˜²ä½ ä¸€ä¸‹å­æ²¡è®¤å‡ºæ¥ï¼Œ`2147483647`æ˜¯ 2^31 ä¸€å·ã€‚**å¦‚æœ**æˆ‘åˆ›å»ºäº†ä¸€ä¸ªæœ‰ 2g æ¡ç›®çš„`Token_List`(æ€€ç–‘å®ƒæ˜¯å¦é€‚åˆå †æ ˆ...æ— è®ºå¦‚ä½•...)**å’Œ**æˆ‘åŠ æ»¡äº†ï¼Œ**ç„¶å**æˆ‘*ä¼š*å®é™…ä¸Šæœ‰æº¢å‡ºã€‚

ç°åœ¨æˆ‘æœ‰ä¸¤ä¸ªé€‰æ‹©:ç¬¬ä¸€ä¸ªæ˜¯è¯´æˆ‘æ°¸è¿œä¸ä¼šä½¿ç”¨ 2g çš„æ¡ç›®ï¼Œå¹¶å¿½ç•¥å¤§å°å†™ï¼›ç¬¬äºŒä¸ªæ˜¯ä½¿å®ƒæˆä¸ºå®˜æ–¹çš„ï¼Œä½ æœ€å¤šå¯ä»¥æœ‰ 2^31 -2 æ¡ç›®ã€‚è¿™çœŸçš„å¾ˆå®¹æ˜“åšåˆ°:åªéœ€åœ¨*è§„æ ¼*æ–‡ä»¶ä¸­ä¿®æ”¹è¡Œ

```
subtype List_Length is Integer range 1 .. Integer'Last; 
```

ç”¨äºå®šä¹‰åˆ—è¡¨é•¿åº¦çš„ç±»å‹ä¸º

```
subtype List_Length is Integer range 1 .. Integer'Last-1; 
```

æ³¨æ„æœ«ç«¯çš„ *-1* ã€‚å› ä¸ºè¿™ä¸ªç±»å‹åœ¨æ„é€ å‡½æ•°
ä¸­ä½¿ç”¨

```
function Create (N : List_Length) return Token_List; 
```

è¿™ä¿è¯äº†ä½ æ°¸è¿œä¸ä¼šç”¨å¤§äº 2^31 -2 çš„`N`æ¥è°ƒç”¨`Create`(å¦‚æœä½ è¯•å›¾è¿™æ ·åšï¼Œä¼šå¼•å‘ä¸€ä¸ªå¼‚å¸¸)ã€‚è¿™å®‰æŠšçš„ç«èŠ±ã€‚

åœ¨æ•æ‰åˆ°è¿™ä¸ª*è¾¹ç•Œçº¿æ¡ä»¶*é”™è¯¯åï¼Œæˆ‘å¼€å§‹è§¦å‘å…¶ä½™çš„ä»£ç ï¼Œæ•æ‰å¦å¤–ä¸¤ä¸ªè¾¹ç•Œçº¿æƒ…å†µã€‚

æ€»çš„æ¥è¯´ï¼Œæˆ‘å¯ä»¥è¯´åŸå§‹ä»£ç å¾ˆå¥½ï¼Œå› ä¸ºé”™è¯¯æ˜¯ç”±ä¸€äº›éå¸¸æç«¯çš„æ¡ä»¶è§¦å‘çš„ï¼›ç„¶è€Œï¼Œå¾ˆé«˜å…´çŸ¥é“ç°åœ¨ä¸€åˆ‡éƒ½å¹²å‡€äº†...